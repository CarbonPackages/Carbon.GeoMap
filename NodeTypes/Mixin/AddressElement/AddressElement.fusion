prototype(Carbon.GeoMap:Mixin.AddressElement) < prototype(Neos.Neos:ContentComponent) {
    name = ${q(node).property('name')}
    street = ${q(node).property('street')}
    city = ${q(node).property('city')}
    country = ${q(node).property('country')}
    lat = ${q(node).property('lat')}
    lng = ${q(node).property('lng')}
    content = ${q(node).property('text')}
    editable = ${node.context.inBackend}


    addressType = 'Carbon.GeoMap:Presentation.Address'
    mapType = ${null}

    @if.hasAddressTypeAndMapType = ${this.addressType && this.mapType}

    renderer = Neos.Fusion:Renderer {
        type = ${props.addressType}
        element = Neos.Fusion:DataStructure {
            @apply.props = ${props}
        }
        @process.wrap = Neos.Fusion:Renderer {
            @if.inCBDModeAndHasCoordinates = ${props.cbd && props.lat && props.lng}
            type = ${props.mapType}
            element.mode = 'edit'
            element.editable = true
            element.coordinates = ${[{'lat': props.lat, 'lng': props.lng}]}
            element.content = ${value}
            element.mapStyle = ${q(node).parent().property('mapStyle')}
            element.inlineEditor = Carbon.GeoMap:Presentation.CoordinatesTable {
                lat = Neos.Neos:Editable {
                    property = 'lat'
                    block = false
                }
                lng = Neos.Neos:Editable {
                    property = 'lng'
                    block = false
                }
            }
        }
    }
}
