prototype(Carbon.GeoMap:Presentation.Map) < prototype(Neos.Fusion:Component) {
    content = null
    class = null
    additionalClass = null

    aspectRatio = '16 / 9'
    effect = null

    js = null
    mjs = null
    css = null
    zoom = null

    coordinates = null

    // https://github.com/sandstorm/Sandstorm.CookiePunch is installed
    _sandstormCookiePunchIsInstalled = ${Configuration.setting('Neos.Neos.fusion.autoInclude')['Sandstorm.CookiePunch']}
    dataNeverBlock = ${!!this._sandstormCookiePunchIsInstalled}
    center = ${Carbon.Array.check(this.coordinates) ? Carbon.GeoMap.centerFromCoordinates(this.coordinates) : null}

    renderer = afx`
        <div
            class={Carbon.String.merge("carbon-geomap", props.class, props.additionalClass)}
            data-center={Json.stringify(props.center)}
            data-zoom={props.zoom}
        >
            <link @if.set={props.css} rel="stylesheet" href={props.css} data-slipstream />
            <script
                @if.set={props.js}
                src={props.js}
                data-never-block={props.dataNeverBlock}
                data-slipstream defer
            ></script>
            <script
                @if.set={props.mjs}
                src={props.mjs}
                data-never-block={props.dataNeverBlock}
                data-slipstream defer
                type="module"
            ></script>
            <div
                class="carbon-geomap__canvas"
                data-effect={Type.isString(props.effect) ? props.effect : null}
                style={"aspect-ratio:" + props.aspectRatio}>
            </div>
            <div class="carbon-geomap__addresses" @if={props.content}>{props.content}</div>
        </div>
    `
}
