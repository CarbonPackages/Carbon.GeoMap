prototype(Carbon.GeoMap:Presentation.Map) < prototype(Neos.Fusion:Component) {
    globalSettings = null
    content = null
    class = null
    additionalClass = null

    aspectRatio = '16 / 9'
    mapStyle = null

    js = null
    css = null

    coordinates = null

    settings = Neos.Fusion:DataStructure {
        @process.filter = ${Array.filter(value)}
    }


    @if.hasContent = ${this.content}

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}

        // https://github.com/sandstorm/Sandstorm.CookiePunch is installed
        _sandstormCookiePunchIsInstalled = ${Configuration.setting('Neos.Neos.fusion.autoInclude')['Sandstorm.CookiePunch']}
        dataNeverBlock = ${!!this._sandstormCookiePunchIsInstalled}

        mapData = Neos.Fusion:DataStructure {
            center = ${Carbon.Array.check(props.coordinates) ? Carbon.GeoMap.centerFromCoordinates(props.coordinates) : null}
        }

        renderer = afx`
            <Neos.Fusion:Debug.Console console={Array.filter(Array.concat(props.mapData, props.settings))} />
            <div
                style="visibility: hidden"
                class={Carbon.String.merge('carbon-geomap', props.class, props.additionalClass)}
                data-map={Json.stringify(Array.filter(Array.concat(props.mapData, props.settings)))}
            >
                <link @if.set={props.css} rel="stylesheet" href={props.css} data-slipstream />
                <script
                    @if.set={props.js}
                    src={props.js}
                    data-never-block={props.dataNeverBlock}
                    data-slipstream defer
                    data-settings={props.globalSettings ? Json.stringify(props.globalSettings) : null}
                ></script>
                <div
                    class={BEM.modifier('carbon-geomap__canvas', props.mapStyle)}
                    style={'aspect-ratio:' + props.aspectRatio}>
                    {props.content}
                </div>
            </div>
        `
    }
}
