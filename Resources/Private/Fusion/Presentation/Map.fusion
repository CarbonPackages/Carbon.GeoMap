prototype(Carbon.GeoMap:Presentation.Map) < prototype(Neos.Fusion:Component) {
    content = null
    class = null
    additionalClass = null

    aspectRatio = '16 / 9'
    effect = null

    js = null
    mjs = null
    css = null
    zoom = null

    coordinates = null

    center = ${Carbon.Array.check(this.coordinates) ? Carbon.GeoMap.centerFromCoordinates(this.coordinates) : null}
    slipstream = Neos.Fusion:DataStructure {
        css = ${Configuration.setting('Carbon.GeoMap.slipstream.css')}
        js = ${Configuration.setting('Carbon.GeoMap.slipstream.js')}
        mjs = ${Configuration.setting('Carbon.GeoMap.slipstream.mjs')}
    }

    renderer = afx`
        <div
            class={Carbon.String.merge("carbon-geomap", props.class, props.additionalClass)}
            data-center={Json.stringify(props.center)}
            data-zoom={props.zoom}
        >
            <link
                @if.set={props.css}
                rel="stylesheet"
                href={props.css}
                data-slipstream={props.slipstream.css}
            />
            <script
                @if.set={props.js}
                src={props.js}
                data-slipstream={props.slipstream.js}
                defer
            ></script>
            <script
                @if.set={props.mjs}
                src={props.mjs}
                data-slipstream={props.slipstream.mjs}
                defer
                type="module"
            ></script>
            <div
                class="carbon-geomap__canvas"
                data-effect={Type.isString(props.effect) ? props.effect : null}
                style={"aspect-ratio:" + props.aspectRatio}>
            </div>
            <div class="carbon-geomap__addresses" @if={props.content}>{props.content}</div>
        </div>
    `
}
